description = 'GridWars - Core Project'

subprojects {
    // Extra properties shared by all sub-projects.
    ext {
        groovyVersion = '2.4.14'
    }

    // Defining buildscript and project dependency repositories for all subprojects.
    buildscript {
        repositories {
            aisNexusPublic()
        }
    }

    // Project dependencies, simply the same as the buildscript repos to avoid repetition.
    repositories {
        addAll(buildscript.repositories)
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'cern.ais.gridwars'
    version = '2.0-SNAPSHOT'

    // Specify the Java version uniformly
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // Puts sub-project name and version in the MANIFEST.MF files of every JAR and WAR file.
    tasks.withType(Jar) {
        manifest {
            attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'CERN / AIS',
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})"
            )
        }
    }
}
